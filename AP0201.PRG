SELECT 1
USE APPERSON INDEX APPERSO2
SELECT 2
USE APCONPER INDEX APCONPER
SELECT 3
USE APGRUPOS INDEX APGRUPOS
SELECT 4
USE APNOMINA INDEX APNOMINA
SELECT 5
USE APPAGGEN INDEX APPAGGE1,APPAGGE2,APPAGGE3
SELECT 6
USE APPAGCON INDEX APPAGCO1,APPAGCO2
SELECT 7
USE APCON    INDEX APCON
STORE .T. TO WABRIENDO
DO WHILE WABRIENDO
   @ 4,0 CLEAR
   @ 04,00 SAY "APERTURA DE PERIODO DE PAGO"
   @ 5,0 TO 18,60
   @ 06,02 SAY "GRUPO DE NOMINAS  :"
   @ 08,02 SAY "CODIGO DE NOMINA  :"
   @ 10,02 SAY "LAPSO DE APERTURA :"
   @ 12,02 SAY "ULTIMO PERIODO    :"
   @ 14,02 SAY "ESTADO ACTUAL     :"
   @ 16,02 SAY "NUEVO PERIODO     :"
   STORE .T. TO WGETGRUPO
   DO WHILE WGETGRUPO
      @ 06,21 GET WGRUPO
      READ
      IF WGRUPO = SPACE(2) .OR. READKEY()=12 .OR. READKEY()=268
         STORE .F. TO WABRIENDO
         EXIT
      ENDIF
      SELECT 3
      FIND &WGRUPO
      IF EOF()
         STORE "GRUPO DE NOMINAS NO REGISTRADO, VERIFIQUE" TO MES
         DO AVISO WITH MES
         LOOP
      ELSE
         @ 06,25 SAY DESCRI
         EXIT
      ENDIF
   ENDDO
   IF .NOT. WABRIENDO
      EXIT
   ENDIF

   STORE .T. TO WGETNOMI
   DO WHILE WGETNOMI
      @ 08,21 GET WNOMINA
      READ
      IF WNOMINA = SPACE(2) .OR. READKEY()=12 .OR. READKEY()=268
         STORE .F. TO WGETNOMI
         EXIT
      ENDIF
      STORE WGRUPO+WNOMINA TO WCLAVENOM
      SELECT 4
      FIND &WCLAVENOM
      IF EOF()
         STORE "NOMINA NO REGISTRADA PARA ESTE GRUPO, VERIFIQUE" TO MES
         DO AVISO WITH MES
         LOOP
      ELSE
         DO RECLOC
         @ 08,25 SAY DESCRI
         STORE LAPSO TO WLAPSO
         EXIT
      ENDIF
   ENDDO
   IF .NOT. WGETNOMI
      LOOP
   ENDIF
   *** DETERMINAR EL ESTADO POR ESCRITO
   STORE ESTADO TO WESTADO
   STORE SPACE(1) TO WESTADODES
   DO ESTADO
   ***
   @ 10,21 SAY STR(LAPSO,3,2)+" DIAS"
   @ 12,21 SAY DTOC(LASTAPER1)+" AL "+DTOC(LASTAPER2)
   @ 14,21 SAY STR(ESTADO,1)+" "+WESTADODES
   STORE "ES ESTA LA NOMINA QUE DESEA ABRIR ? (S/N)" TO TEX
   STORE "SN" TO WCH
   DO PREGUNTA
   IF WCH = "N"
      LOOP
   ENDIF
   IF ESTADO <> 0
      @ 16,21 SAY DTOC(APER1)+" AL "+DTOC(APER2)
      STORE "ATENCION: NOMINA NO CERRADA, ABANDONAR DATOS DE APERTURA ACTUAL ? (S/N)" TO TEX
      STORE "NS" TO WCH
      DO PREGUNTA
      IF WCH = "N"
         LOOP
      ENDIF
      STORE APER1       TO WAPER1
      STORE APER2       TO WAPER2
   ELSE
      STORE APER2+1     TO WAPER1
      STORE APER2+LAPSO TO WAPER2
   ENDIF
   @ 16,21 GET WAPER1
   @ 16,33 GET WAPER2
   READ
   STORE "OPCIONES: (C)ONTINUAR, (S)ALIR" TO TEX
   STORE "CS" TO WCH
   DO PREGUNTA
   IF WCH = "S"
      UNLOCK ALL
      LOOP
   ENDIF
   *** ELIMINA TODOS LOS SOBRES DE LA NOMINA QUE SE VA A ABRIR
   STORE "ELIMINANDO POSIBLES DATOS DE APERTURA ANTERIOR" TO MES
   DO MENSAJE WITH MES
   STORE WGRUPO+WNOMINA TO WCLAGEN
   SELECT 5
   SET ORDER TO 2
   FIND &WCLAGEN
   DO WHILE .NOT. EOF() .AND. GRUPO = WGRUPO .AND. NOMINA = WNOMINA
      STORE CEDULA TO WDELCED
      DO RECLOC
      DELETE
      UNLOCK
      SELECT 6
      STORE .T. TO WDELETING
      DO WHILE WDELETING
         FIND &WDELCED
         IF EOF()
            EXIT
         ENDIF
         DO RECLOC
         DELETE   
         UNLOCK
      ENDDO
      SELECT 5
      SKIP
   ENDDO
   SELECT 5
   SET ORDER TO 1
   *** FIN ELIMINACION
   STORE "EFECTUANDO APERTURA DE NOMINA, FAVOR ESPERAR..." TO MES
   DO MENSAJE WITH MES
   SELECT 1
   FIND &WCLAVENOM
   DO WHILE .NOT. EOF() .AND. GRUPO = WGRUPO .AND. NOMINA = WNOMINA
      STORE CEDULA TO XCEDULA
      IF ESTATUS = "I" .or. ESTATUS = "V"
         SELECT 1
         SKIP
         LOOP
      ELSE
         IF ESTATUS <> "A"
            STORE "ATENCION: ESTATUS INVALIDO CEDULA:"+XCEDULA+" (C)ONTINUAR, (R)ECHAZAR APERTURA" TO TEX
            STORE "CR" TO WCH
            IF WCH = "R"
               STORE .F. TO WABRIENDO
               EXIT
            ENDIF
         ENDIF
      ENDIF
      *** ELIMINA POSIBLES REGISTROS DE PAGO ANTERIORES A LA APERTURA ACTUAL
      STORE APPERSON->CEDULA TO WCLAVECED
      SELECT 5
      STORE .T. TO WDELETE
      DO WHILE WDELETE
         FIND &WCLAVECED
         IF EOF()
            EXIT
         ENDIF
         DO RECLOC
         DELETE
         UNLOCK
      ENDDO
      SELECT 6
      STORE .T. TO WDELETE
      DO WHILE WDELETE
         FIND &WCLAVECED
         IF EOF()
            EXIT
         ENDIF
         DO RECLOC
         DELETE
         UNLOCK
      ENDDO
      ***
      *** DAR VALOR INICIAL A VARIALBLES DE PAGO
      STORE 0 TO WTOTBON
      STORE 0 TO WTOTNBON
      STORE 0 TO WTOTASI
      STORE 0 TO WTOTDEC
      STORE 0 TO WTOTLIQ
      ***
      *** CREA REGISTRO DE PAGO MAESTRO EN APPAGGEN
      *** DEBERIA PRIMERO BUSCAR EL CONCEPTO QUE QUIERE INCLUIR, SI LO
      *** ENCUENTA, SOLO DEBE REEMPLAZAR SIN UTILIZAR EL APPEND.
      SELECT 5
      DO FILLOC
      APPEND BLANK
      UNLOCK
      DO RECLOC
      REPLACE CEDULA WITH APPERSON->CEDULA
      REPLACE FECHA1 WITH WAPER1
      REPLACE FECHA2 WITH WAPER2
      REPLACE GRUPO  WITH APPERSON->GRUPO
      REPLACE NOMINA WITH APPERSON->NOMINA
      REPLACE DIRE   WITH APPERSON->DIRE
      REPLACE DEPA   WITH APPERSON->DEPA
      REPLACE SECC   WITH APPERSON->SECC
      REPLACE CARGO  WITH APPERSON->CARGO
      REPLACE SUELDOH WITH APPERSON->SUELDOH
      REPLACE SUELDOD WITH APPERSON->SUELDOD
      REPLACE SUELDOP WITH APPERSON->SUELDOP
      REPLACE TIPO   WITH APPERSON->TIPO
      UNLOCK
      FLUSH
      STORE "CEDULA: "+APPERSON->CEDULA TO MES
      DO MENSAJE WITH MES
      *** CREA REGISTRO PARA CADA CONCEPTO DE LA PERSONA (SEGUN FRECUENCIA)
      * DETERMINA SI ES EL ULTIMO PERIODO DEL MES
      STORE WAPER2+WLAPSO TO WPROXAPER2
      IF MONTH(WAPER2) <> MONTH(WPROXAPER2)
         STORE .T. TO WULTLAPSO
      ELSE
         STORE .F. TO WULTLAPSO
      ENDIF
      ***
      SELECT 2
      FIND &WCLAVECED
      DO WHILE .NOT. EOF() .AND. CEDULA = WCLAVECED
         STORE WCLAVECED          TO XCEDULA
         STORE APCONPER->CONCEPTO TO XCONCEPTO
         STORE APCONPER->CANTIDAD TO XCANTIDAD
         *** FILTRO DE CONCEPTOS DE FIN DE MES Y EVENTUALES
         SELECT 7
         FIND &XCONCEPTO
         IF EOF()
            *** CONCEPTO NO DEFINIDO
            SELECT 2
            SKIP
            LOOP
         ELSE
            STORE FRECUENCIA TO XFRECUEN
         ENDIF
         IF XFRECUEN > 0 .AND. .NOT. WULTLAPSO
            SELECT 2
            SKIP
            LOOP
         ENDIF
         *** FIN FILTRO DE FIN DE MES Y EVENTUALES
         SELECT 6
         STORE XCEDULA+STR(YEAR(WAPER2))+STR(MONTH(WAPER2))+STR(DAY(WAPER2))+XCONCEPTO TO XCLAVE
         FIND &XCLAVE
         IF EOF()
            DO FILLOC
            APPEND BLANK
            REPLACE CEDULA WITH XCEDULA
            REPLACE FECHA1 WITH WAPER1
            REPLACE FECHA2 WITH WAPER2
            REPLACE CONCEPTO WITH XCONCEPTO
            UNLOCK
         ENDIF
         DO RECLOC
         REPLACE CANTIDAD WITH XCANTIDAD
         FLUSH
         UNLOCK
         SELECT 2
         SKIP
      ENDDO
      *** CALCULO EN BS. DEL SOBRE
      *STORE WCLAVECED TO XCEDULA
      DO SOBRECAL
      IF WTOTLIQ < 0
         STORE "ATENCION: SOBRE DE :"+XCEDULA+" CON SALDO NEGATIVO" TO MES
         DO AVISO WITH MES
      ENDIF
      SELECT 1
      SKIP
   ENDDO
   IF .NOT. WABRIENDO
      LOOP
   ELSE
      *** FIJAR NUEVOS VALORES DE LA NOMINA
      SELECT 4
      REPLACE LASTAPER1 WITH APER1
      REPLACE LASTAPER2 WITH APER2
      REPLACE APER1     WITH WAPER1
      REPLACE APER2     WITH WAPER2
      REPLACE ESTADO    WITH 1
      UNLOCK
      FLUSH
      ***
      STORE "APERTURA EFECTUADA SATISFACTORIAMENTE, OPRIMA <ENTER>" TO MES
      DO AVISO WITH MES
   ENDIF
   UNLOCK ALL
ENDDO
CLOSE DATA
CLOSE INDEX
RETURN

